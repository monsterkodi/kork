###
00     00   0000000   000000000  00000000  00000000   000   0000000   000       0000000
000   000  000   000     000     000       000   000  000  000   000  000      000     
000000000  000000000     000     0000000   0000000    000  000000000  000      0000000 
000 0 000  000   000     000     000       000   000  000  000   000  000           000
000   000  000   000     000     00000000  000   000  000  000   000  0000000  0000000 
###

material = (color, cfg) ->
    
    cfg.color = color 
    clss = THREE.MeshStandardMaterial
    switch cfg.typ 
        'lambert' ➜ clss = THREE.MeshLambertMaterial
        'basic'   ➜ clss = THREE.MeshBasicMaterial
        'shadow'  ➜ clss = THREE.ShadowMaterial
        'toon'    ➜ clss = THREE.MeshToonMaterial
    delete cfg.typ
    new clss cfg

Materials = 
    misc:
        transparent:    material 0x888888, typ:'lambert' depthWrite:false, transparent:true, opacity:0.1
        flat:           material 0xffffff, metalness: 0.5 roughness: 0.7 flatShading:true, dithering:true
        toon:           material 0xffffff, typ:'toon'
    shinyblack:         material 0x000000, metalness: 0.6 roughness: 0.1 flatShading:true
    player:             material 0x8888ff, metalness: 0.0 roughness: 0.1 dithering:true #emissive:0x222288
    wireframe:          material 0x888888, typ:'basic' wireframe: true
    floor:              material 0x020202, metalness: 0.2, roughness: 0.8 dithering:true
    landscape:          material 0x444444, metalness: 0.2, roughness: 0.8 dithering:true
    shadow:             material 0x000000, typ:'shadow' opacity:0.2 depthWrite:false
    station:
        side:           material 0x000000, metalness: 0.5 roughness: 0.2, dithering:true, side:THREE.DoubleSide
    menu:
        active:         material Colors.menu.active,       metalness: 0.2, roughness: 0.5, emissive: Colors.menu.active
        inactive:       material Colors.menu.inactive,     metalness: 0.9, roughness: 0.75
        activeHigh:     material Colors.menu.activeHigh,   metalness: 0.9, roughness: 0.75
        inactiveHigh:   material Colors.menu.inactiveHigh, metalness: 0.9, roughness: 0.75

    setWire: (wire) ->
        
        prefs.set 'wire' wire
        for t in ['floor' 'landscape']
            Materials[t].wireframe = wire
        
    getWire: -> prefs.get 'wire'
    toggleWire: -> Materials.setWire not Materials.getWire()

    setFlat: (flat) ->
        
        prefs.set 'flat' flat
        for t in ['floor' 'landscape']
            Materials[t].flatShading = flat
            Materials[t].needsUpdate = true
            
    getFlat: -> prefs.get 'flat'
    toggleFlat: -> Materials.setFlat not Materials.getFlat()

module.exports = Materials
